buildscript {
    ext.versions = [
            'minSdk'    : 14,
            'compileSdk': 27,
            'kotlin'    : '1.1.60',
            'lint'      : '26.0.1'
    ]

    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"

        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
        /*maven {
            url "https://dl.bintray.com/ggg1234567/maven"
        }*/
    }

    group = GROUP
    version = VERSION_NAME
}

ext {
    // Android dependencies.
    supportV4 = 'com.android.support:support-v4:27.0.0'
    supportAnnotations = 'com.android.support:support-annotations:27.0.0'
    supportTestRunner = 'com.android.support.test:runner:0.5'

    // https://developer.android.com/topic/libraries/architecture/adding-components.html
    androidArchViewModel = 'android.arch.lifecycle:viewmodel:1.1.0'

    supportSqlite = 'android.arch.persistence:db:1.0.0'
    supportSqliteFramework = 'android.arch.persistence:db-framework:1.0.0'

    // https://www.zetetic.net/sqlcipher/sqlcipher-for-android/
    supportSqlcipher = 'net.zetetic:android-database-sqlcipher:3.5.8@aar'
    sqlbrite_sqlcipher_integration = 'android.arch.persistence.db.sqlcipher:sqlbrite-sqlcipher-integration:1.1.1'

    // https://github.com/Tencent/wcdb
    supportWcdb = 'com.tencent.wcdb:wcdb-android:1.0.5'
    sqlbrite_wcdb_integration = 'android.arch.persistence.db.wcdb:sqlbrite-wcdb-integration:1.1.1'

    // Third-party dependencies.
    kotlinStdLib = "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
    dagger = 'com.google.dagger:dagger:2.13'
    daggerCompiler = 'com.google.dagger:dagger-compiler:2.13'
    butterKnifeRuntime = 'com.jakewharton:butterknife:8.8.1'
    butterKnifeCompiler = 'com.jakewharton:butterknife-compiler:8.8.1'
    timber = 'com.jakewharton.timber:timber:4.6.0'
    autoValue = 'com.google.auto.value:auto-value:1.5'
    autoValueParcel = 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'
    rxJava = 'io.reactivex.rxjava2:rxjava:2.1.3'
    rxAndroid = 'io.reactivex.rxjava2:rxandroid:2.0.1'
    rxBinding = 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    junit = 'junit:junit:4.12'
    truth = 'com.google.truth:truth:0.36'

    // Lint dependencies.
    lintApi = "com.android.tools.lint:lint-api:${versions.lint}"
    lint = "com.android.tools.lint:lint:${versions.lint}"
    lintTests = "com.android.tools.lint:lint-tests:${versions.lint}"
}

configurations {
    osstrich
}
dependencies {
    osstrich 'com.squareup.osstrich:osstrich:1.2.0'
}
task publishV1Javadoc(type: JavaExec) {
    classpath = configurations.osstrich
    main = 'com.squareup.osstrich.JavadocPublisher'
    args = [
            'build/javadoc',
            'https://github.com/square/sqlbrite',
            'com.squareup.sqlbrite'
    ]
}
task publishV2Javadoc(type: JavaExec) {
    classpath = configurations.osstrich
    main = 'com.squareup.osstrich.JavadocPublisher'
    args = [
            'build/javadoc',
            'https://github.com/square/sqlbrite',
            'com.squareup.sqlbrite2'
    ]
}
task publishV3Javadoc(type: JavaExec) {
    classpath = configurations.osstrich
    main = 'com.squareup.osstrich.JavadocPublisher'
    args = [
            'build/javadoc',
            'https://github.com/square/sqlbrite',
            'com.squareup.sqlbrite3'
    ]
}
task publishJavadoc(dependsOn: [publishV1Javadoc, publishV2Javadoc, publishV3Javadoc])
